apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = "com.foodtruck"
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroup 'core.framework'
            }
        }
    }
}


def coreNGVersion = '7.5.12'

configure(subprojects.findAll { it.name.endsWith('-interface') || it.name.matches(/(-|\w)+-interface-v\d+$/) }) {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure(subprojects.findAll {
    it.name.endsWith('-service') || it.name.matches(/(-|\w)+-service-v\d+$/) || it.name.endsWith('-api') || it.name.endsWith('-site')
}) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

// services use mongodb
configure([project(':reptile-service')]) {
    dependencies {
        implementation project(":reptile-service-interface")
    }
}